WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ident_syms = _{ "_" | "-" | "'" }
identifier = @{ ASCII_ALPHA+ ~ (ident_syms | ASCII_ALPHA)* }

bitwidth = @{ ASCII_DIGIT+ }
num_lit = @{ ASCII_DIGIT+ } // TODO: add verilog style number literals

// ====== toplevel ======

component = {
      "component" ~ identifier ~ signature?
      ~ "{"
      ~ cells
      ~ connections
      ~ control
      ~ "}"
}

file = {
      SOI
      ~ component+
      ~ EOI
}

// ====== signature ======

signature = {
      "(" ~ io_ports? ~ ")" ~ ("->" ~ "(" ~ io_ports? ~ ")")?
}

io_port = {
      identifier ~ ":" ~ bitwidth
}

io_ports = {
      io_port ~ ("," ~ io_port)*
}

args = {
      "(" ~ (bitwidth ~ ("," ~ bitwidth)*)? ~ ")"
}

// ====== cells ======

primitive_cell = {
      identifier ~ "=" ~ "prim" ~ identifier ~ args
}

component_cell = {
      identifier ~ "=" ~ identifier
}

cells = {
      "cells"
      ~ "{" ~ ((primitive_cell | component_cell) ~ ";")* ~ "}"
}

// ====== wires ======

hole = {
      identifier ~ "[" ~ identifier ~ "]"
}

port = {
      (identifier ~ "." ~ identifier)
    | identifier
}

LHS = { hole | port }
expr = { LHS | num_lit }

comparator = { "==" | "!=" | "<" | ">" | "<=" | ">=" }

guard_expr = {
      (expr ~ comparator ~ expr) | expr
}

guard = {
      guard_expr ~ ("&" ~ guard_expr)*
}

switch_stmt = {
      guard ~ "=>" ~ expr ~ ";"
}

switch = {
      "switch" ~ "{"
      ~ switch_stmt+
      ~ "}"
}

wire = {
      LHS ~ "=" ~ (switch | (expr ~ ";"))
}

group = {
      "group" ~ identifier ~ "{"
      ~ wire*
      ~ "}"
}

connections = {
      "wires"
      ~ "{"
      ~ (wire | group)*
      ~ "}"
}

// ====== control ======

enable = { identifier ~ ";" }

seq = {
      "seq" ~ "{"
      ~ stmt*
      ~ "}"
}

par = {
      "par" ~ "{"
      ~ stmt*
      ~ "}"
}

block = _{
      "{" ~ stmt ~ "}"
}

if_cond = {
      ("with" ~ identifier)?
}

if_stmt = {
      "if" ~ port ~ if_cond ~ block ~ ("else" ~ (if_stmt | block))?
}

while_stmt = {
      "while" ~ port ~ ("with" ~ identifier)? ~ block
}

stmt = {
      enable
    | (seq ~ ";"?)
    | (par ~ ";"?)
    | (if_stmt ~ ";"?)
    | (while_stmt ~ ";"?)
}

control = {
      "control"
      ~ "{"
      ~ stmt
      ~ "}"
}
